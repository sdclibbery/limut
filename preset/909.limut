preset bd909 pitchedperc
	, dur=1/4, amp*=this.level*5/3
	, choke=this.player
  , lpf=3000
  // 909 BD Controls
  , level=1/2
  , tune=1/2
  , attack=1/2
  , decay=1/2
  // Derived params
  , accent=(this.value+this.add)/7
  // Pitched Perc params
	, click= 3/2*(this.attack*2 + this.accent)
  , hit= 0
	, body={ 1 + this.accent
    , wave:'tri'
    , att:10ms, dec:200ms+1800ms*this.decay
    , freq:47, boost:120, pitchatt:4ms, pitchdec:10ms+200ms*this.tune
    , saturation:0.85
  }
  , rattle={ 3*(this.attack*3 + this.accent)
    , dec:400ms
    , freq:47, boost:153, pitchatt:4ms, pitchdec:10ms+200ms*this.tune
  }

preset h909 sample
	, sample='sample/909/hh.wav'
  // 909 HiHat controls
  , level=1/2
  , chdecay=1/2
  , ohdecay=1/2
  // Derived params
  , decay=30ms + ((this.ohdecay*1000ms+400ms)*this.o) + (this.chdecay*180ms*(1-this.o))
  , accent=(this.value+this.add)/7
  // Sample synth params
  , dur=1/4, sus=max{this.dur,this.decay}, choke=this.player
  , rate=1
  , envelope='organ'
  , amp*=envelope{d:this.decay}*(this.level+this.accent)*0.7/3

preset cp909 io808, type='cp', choke=this.player, amp*=2 // 909 uses similar handclap as 808

preset cc909 sample
	, sample='sample/909/crash.wav'
  // 909 Crash controls
  , level=1/2
  , tune=1/2
  // Derived params
  , decay=2000
  , accent=(this.value+this.add)/7
  // Sample synth params
  , dur=1/4, sus=this.decay, choke=this.player
  , rate=3/4+this.tune/2
  , envelope='organ'
  , amp*=(this.level+this.accent)*0.6

preset rc909 sample
	, sample='sample/909/ride.wav'
  // 909 Ride controls
  , level=1/2
  , tune=1/2
  // Derived params
  , decay=2000
  , accent=(this.value+this.add)/7
  // Sample synth params
  , dur=1/4, sus=this.decay, choke=this.player
  , rate=3/4+this.tune/2
  , envelope='organ'
  , amp*=(this.level+this.accent)*0.2

preset sd909 pitchedperc
	, dur=1/4, amp*=this.level*2/3
	, choke=this.player
  // 909 SD Controls
  , level=1/2
  , tune=1/2
  , tone=1/2
  , snappy=1/2
  // Derived params
  , accent=(this.value+this.add)/9
  // Pitched Perc params
	, click= 0
  , hit= 0
	, body={ 1 + this.accent
    , wave:'tri'
    , att:10ms, dec:260ms
    , freq:110+177*this.tune, boost:100, pitchatt:4ms, pitchdec:40ms
    , saturation:0.85
  }
	, body2={ 1/2 + this.accent/2
    , wave:'tri'
    , att:10ms, dec:260ms
    , freq:139+107*this.tune, boost:133, pitchatt:4ms, pitchdec:40ms
    , saturation:0.85
  }
  , rattle={ 3/2*(this.snappy + this.accent)
    , dec:200ms+600ms*this.tone
    , filter:'bandpass', freq:[2500:4000]e0.4, boost:153, pitchatt:4ms, pitchdec:400ms, q:0.2 // Real 909 splits noise signal and has low and high pass not bandpass
  }

preset t909 pitchedperc
	, dur=1/4, amp*=this.level*4/3
	, choke=this.player
  // 909 Toms Controls
  , level=1/2
  , tune=1/2
  , decay=1/2
  // Derived params
  , accent=(this.value+this.add)/9
  , freq=(80 - 25*this.l + 35*this.h)*2/3 * (1+this.tune)
  // Pitched Perc params
	, click= 0
  , hit= 0
	, body={ 1 + this.accent
    , wave:'tri'
    , att:10ms, dec:500ms*(1+this.decay)
    , freq:this.freq, boost:this.freq, pitchatt:4ms, pitchdec:300ms
    , saturation:0.85
  }
	, body2={ 1 + this.accent
    , wave:'tri'
    , att:10ms, dec:200ms
    , freq:this.freq*1.13, boost:this.freq*1.73, pitchatt:4ms, pitchdec:120ms
    , saturation:1 // Real 909 has changing saturation over time
  } // Real 909 has a third body tone
  , rattle={ 1/2*(1 + this.accent)
    , dec:100ms
    , filter:'bandpass', freq:500, boost:200, pitchatt:0ms, pitchdec:60ms, q:[0.1:1]e0.05
  }

preset rs909 impulse
	, dur=1/4, amp*=(this.level + this.accent)*20
	, choke=this.player
  , envelope='organ'
  , accent=(this.value+this.add)/9
  // 909 Rimshot Controls
  , level=1/2
  // Impulse params: 3 resonant bandpass filters simulating the bridged-t networks in the 909 rimshot
  , bpf1={220,q:10.5}
  , bpf2={500,q:10.5}
  , bpf3={1000,q:10.5}

//------------------------------------------------------------------------------------
// Sample based 909 (very inaccurate)

preset bds909 sample, dur=1/4
	, sample='sample/909/limut909bd.wav'
  // Sample synth params
	, sus=1/8+1/2*this.decay, choke=this.player
  , rate=1 + 1/3*(this.tune-1/2)
  , envelope='organ', att=0
	, amp*=3 * this.level * (1 + this.accent + this.attack)
	, mid=0.2 + 1.6*this.attack
	, high=0.2 + 1.6*this.attack
  // 909 BD Controls
  , level=1/2
  , tune=1/2
  , attack=1/2
  , decay=1/2
  // Derived params
  , accent=(this.value+this.add)/7


preset hs909 h909

preset cps909 sample
	, sample='sample/909/limut909cp.wav'
  // Sample synth params
  , dur=1/4, choke=this.player, sus=1
  , rate=1
  , envelope='organ', att=0
	, amp*=2 * this.level * (1 + this.accent)
  // 909 CP Controls
  , level=1/2
  // Derived params
  , accent=(this.value+this.add)/9

preset ccs909 cc909

preset rcs909 rc909

preset sds909 sample
	, sample='sample/909/limut909sd.wav'
  // Sample synth params
  , dur=1/4, choke=this.player, sus=1
  , rate=1
  , envelope='organ', att=0
  , amp*=4*envelope{d:100ms+1200ms*(this.tone+this.snappy)}*(this.level + this.accent)
	, mid=0.2 + 0.8*(this.tone+this.snappy)
	, high=0.2 + 3.2*this.tone*this.snappy
  , accent=(this.value+this.add)/9
  // 909 SD Controls
  , level=1/2
  , tune=1/2
  , tone=1/2
  , snappy=1/2

preset ts909 sample
	, sample='sample/909/limut909t.wav'
  // Sample synth params
  , dur=1/4, choke=this.player, sus=1
  , rate=this.freq/80
  , envelope='organ', att=0
  , amp*=1.8*(this.level+this.accent/2)*envelope{d:250ms*(1+20*this.decay)}
  // 909 Toms Controls
  , level=1/2
  , tune=1/2
  , decay=1/2
  // Derived params
  , accent=(this.value+this.add)/9
  , freq=(80 - 25*this.l + 35*this.h)*2/3 * (1+this.tune)

preset rss909 sample
	, sample='sample/909/limut909rs.wav'
  // Sample synth params
  , dur=1/4, choke=this.player, sus=1
  , rate=1
  , envelope='organ', att=0
  , amp*=5*(this.level+this.accent/2)
  // 909 rs Controls
  , level=1/2
  // Derived params
  , accent=(this.value+this.add)/9
