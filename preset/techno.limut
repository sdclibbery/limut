include 'lib/effects.limut'
include 'preset/909.limut'
include 'preset/303.limut'
include 'preset/trance.limut'

preset tinbass dbass
  , dur=1/4, att=0.01, rel=0.01, pan=[-1/2:1/2]r
  , lpf={(this.cutoff*this.vel+1/2)*this.freq*[20,5]e*[1:2]s10,q:10}
  , echo=1.01, room=1, flanger=1/16
  , cutoff=1/2

preset hollow tri
  , envelope='pad', addc=wow
  , oct+=(-1,0), add+=(0,4)
  , amp*=[1/3:3/2]l[8.0]@f
  , bpf=[500:2000]n8, echo=1.01, chorus=3, phaser=1/23
  , noisify=[1/16:1/4]n12

preset sweep saw, dur=4, envelope='pad', lpf={(1/2+this.cutoff*this.vel)*[220:880]s8@f,q:16,poles:4}, oct=2, amp*=1/2, glide=2, cutoff=1/2

preset robot wave
  , wave='saw', dur=1/4, envelope='percussion', oct=3
  , noisify=1/8, ring=this.freq*3, addc=[0,1]
  , bpf={[700:1500]n0.67,q:[0.5:4]n0.87 ^2}, phaser=1/3
  , amp*=0.65 * [1:2]n0.23
  , room={1,hpf:500,mix:1/2}, drive=1/8

preset techbass saw
  , envelope='percussion', dec=1/4+this.decay/2, pan=[-1/4:1/4]r
  , lpf={10+3000*[1:!200ms+1000ms*this.decay^2,0]es*(0.15+2*this.vel*this.cutoff^3),q:9}, oct=2, dur=1/4
  , chorus=1/3
  // Controls
  , decay=[1/8:3/4]l8
  , cutoff=[0:1]l4

preset psy multiwave
  , envelop='organ', oct=4, amp*=3/2, dur=1/4
  , wave1={'saw'}
  , wave2={'saw',amp:1/2,detune:-12}
	, bpf={250+2250*this.vel*this.cutoff^2,q:3,poles:4}, high=-3db
  , drive=1/8, delay=1/8, room={1,hpf:500,mix:1/2}, glide=this.dur/10
  , cutoff=[2/3:0]e + [0:1/3]l1/2@f + [0:1/3]l2@f

preset sub sine, dur=1/4, sus=1/6, oct=1, envelope='organ', amp*=3, amp*=[1:!400ms,0]es, drive={1/32,mix:1/6}

preset growl square, dur=1, oct=2, envelope='percussion', fx=ultracomb>>{dry,flanger>>pingpong>>reverb{2}}*1.3
