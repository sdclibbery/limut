preset tb303 wave
	, wave='saw', oct=2+this.u-this.d, choke=this.player
	, dur=1/4 // 1/16th notes
  , envelope='organ', sus={this.dur-(1-this.a)*5/48, level:1} // Gate
  , amp*=envelope{d:1000, units:'ms', shape:'exp'}*3/4 // VCA envelope: slow exponential decay
  // 303 Controls
  , cutoff=1/2
  , resonance=1/2
  , envmod=1/2
	, decay=1/2
  , accent=1/2
  // Derived params
  , amp*=3/4 + this.a*this.accent/3
  , meg=envelope{d:340+(this.decay^2)*(1-this.a)*2000, units:'ms', shape:'exp'} // 303 Main Envelope Generator
  , smoothedmeg=smooth{this.meg*this.a*128,att:2^-6,dec:2^-16+(1-this.a)*100}*this.resonance + this.meg*(1-this.resonance) // Smoothed meg for the accent sweep, based on resonance
  , accentedmeg=this.smoothedmeg*this.a*this.accent + this.meg*(1-this.a*this.accent) // Accent sweep uses smoothed meg
  , appliedmeg=(this.envmod+1)*this.accentedmeg + 1-this.envmod // Apply envmod
  , lpf={
    80 + (160 + 1700*this.cutoff^3) * this.appliedmeg,
    q:this.resonance*25,
    poles:4
  }
  , glide={(this.g+this.s)*88, units:'ms'}

preset m1organ multiwave
	, dur=1/4 // 16th notes for syncopation
  , envelope='organ', rel=1/2, amp*=3
  , cutoff=1/2
  , wave1={'tri',detune:-24}
  , wave2={'square',detune:7,amp:1/4}
  , wave3={'noise',detune:-3*12,amp:envelope{d:10,shape:'exp'}/2}
  , lpf={500*(1/2+this.cutoff)*(1+envelope{d:500,shape:'exp'}),poles:4,q:10}

preset bd909 pitchedperc
	, dur=1/4, amp*=this.level*4/3
	, choke=this.player
  , lpf=3000
  // 909 BD Controls
  , level=1/2
  , tune=1/2
  , attack=1/2
  , decay=1/2
  // Derived params
  , accent=(this.value+this.add)/7
  // Pitched Perc params
	, click= 3/2*(this.attack + this.accent)
  , hit= 0
	, body={ 1 + this.accent
    , wave:'tri'
    , att:5, dec:100+900*this.decay
    , freq:47, boost:120, pitchatt:2, pitchdec:5+100*this.tune
    , saturation:0.85
  }
  , rattle={ 3*(this.attack + this.accent)
    , dec:200
    , freq:47, boost:153, pitchatt:2, pitchdec:5+100*this.tune
  }

preset h909 sample
	, sample='sample/909/hh.wav'
  // 909 HiHat controls
  , level=1/2
  , chdecay=1/2
  , ohdecay=1/2
  // Derived params
  , decay=30 + ((this.ohdecay*1000+400)*this.o) + (this.chdecay*180*(1-this.o))
  , accent=(this.value+this.add)/7
  // Sample synth params
  , dur=1/4, sus=max{this.dur,this.decay}, choke=this.player
  , rate=1
  , envelope='organ'
  , amp*=envelope{d:this.decay, units:'ms', shape:'exp'}*(this.level+this.accent)*0.7/3

preset cp909 io808, type='cp', choke=this.player, amp*=2 // 909 uses similar handclap as 808

preset cc909 sample
	, sample='sample/909/crash.wav'
  // 909 Crash controls
  , level=1/2
  , tune=1/2
  // Derived params
  , decay=2000
  , accent=(this.value+this.add)/7
  // Sample synth params
  , dur=1/4, sus=this.decay, choke=this.player
  , rate=3/4+this.tune/2
  , envelope='organ'
  , amp*=(this.level+this.accent)*0.6

preset rc909 sample
	, sample='sample/909/ride.wav'
  // 909 Ride controls
  , level=1/2
  , tune=1/2
  // Derived params
  , decay=2000
  , accent=(this.value+this.add)/7
  // Sample synth params
  , dur=1/4, sus=this.decay, choke=this.player
  , rate=3/4+this.tune/2
  , envelope='organ'
  , amp*=(this.level+this.accent)*0.2

preset sd909 pitchedperc
	, dur=1/4, amp*=this.level*2/3
	, choke=this.player
  // 909 SD Controls
  , level=1/2
  , tune=1/2
  , tone=1/2
  , snappy=1/2
  // Derived params
  , accent=(this.value+this.add)/9
  // Pitched Perc params
	, click= 0
  , hit= 0
	, body={ 1 + this.accent
    , wave:'tri'
    , att:5, dec:130
    , freq:110+177*this.tune, boost:100, pitchatt:2, pitchdec:20
    , saturation:0.85
  }
	, body2={ 1/2 + this.accent/2
    , wave:'tri'
    , att:5, dec:130
    , freq:139+107*this.tune, boost:133, pitchatt:2, pitchdec:20
    , saturation:0.85
  }
  , rattle={ 3/2*(this.snappy + this.accent)
    , dec:100+300*this.tone
    , filter:'bandpass', freq:[2500:4000]e0.4, boost:153, pitchatt:2, pitchdec:200, q:0.2 // Real 909 splits noise signal and has low and high pass not bandpass
  }

preset t909 pitchedperc
	, dur=1/4, amp*=this.level*4/3
	, choke=this.player
  // 909 Toms Controls
  , level=1/2
  , tune=1/2
  , decay=1/2
  // Derived params
  , accent=(this.value+this.add)/9
  , freq=(80 - 25*this.l + 35*this.h)*2/3 * (1+this.tune)
  // Pitched Perc params
	, click= 0
  , hit= 0
	, body={ 1 + this.accent
    , wave:'tri'
    , att:5, dec:250*(1+this.decay)
    , freq:this.freq, boost:this.freq, pitchatt:2, pitchdec:150
    , saturation:0.85
  }
	, body2={ 1 + this.accent
    , wave:'tri'
    , att:5, dec:100
    , freq:this.freq*1.13, boost:this.freq*1.73, pitchatt:2, pitchdec:60
    , saturation:1 // Real 909 has changing saturation over time
  } // Real 909 has a third body tone
  , rattle={ 1/2*(1 + this.accent)
    , dec:50
    , filter:'bandpass', freq:500, boost:200, pitchatt:0, pitchdec:30, q:[0.1:1]e0.05
  }

