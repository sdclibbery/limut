preset tb303 wave
	, wave='saw', oct=2+this.u-this.d, choke=this.player
	, dur=1/4 // 1/16th notes
  , envelope='full', att=0.01, dec=0, rel=0.01, sus={this.dur-(1-this.a)*5/48, level:1} // Gate
  , amp*=envelope{d:1000, units:'ms', shape:'exp'} // VCA envelope: slow exponential decay
  // 303 Controls
  , cutoff=1/2
  , resonance=1/2
  , envmod=1/2
	, decay=1/2
  , accent=1/2
  // Derived params
  , amp*=3/4 + this.a*this.accent/3
  , meg=envelope{d:340+(this.decay^2)*(1-this.a)*2000, units:'ms', shape:'exp'} // 303 Main Envelope Generator
  , smoothedmeg=smooth{this.meg*this.a*128,att:2^-6,dec:2^-16+(1-this.a)*100}*this.resonance + this.meg*(1-this.resonance) // Smoothed meg for the accent sweep, based on resonance
  , accentedmeg=this.smoothedmeg*this.a*this.accent + this.meg*(1-this.a*this.accent) // Accent sweep uses smoothed meg
  , appliedmeg=(this.envmod+1)*this.accentedmeg + 1-this.envmod // Apply envmod
  , lpf={
    80 + (160 + 1700*this.cutoff^3) * this.appliedmeg,
    q:this.resonance*25,
    poles:4
  }
  , glide={(this.g+this.s)*88, units:'ms'}

preset bd909 pitchedperc
	, dur=1/4, amp*=this.level*2
	, choke=this.player
  , lpf=3000
  // 909 BD Controls
  , level=1/2
  , tune=1/2
  , attack=1/2
  , decay=1/2
  // Pitched Perc params
	, click= 3/2*this.attack
  , hit= 0
	, body={ 1
    , wave:'tri'
    , att:5, dec:100+900*this.decay
    , freq:47, boost:120, pitchatt:2, pitchdec:5+100*this.tune
    , saturation:0.85
  }
  , rattle={ 3*this.attack
    , dec:200
    , freq:47, boost:153, pitchatt:2, pitchdec:5+100*this.tune
  }

preset h909 sample
	, sample='sample/909/hh.wav'
  // 909 HiHat controls
  , chdecay=1/2
  , ohdecay=1/2
  // Sample synth params
  , dur=1/4, choke='h909'
  , envelope='full', att=0, dec=0, sus={1,level:1}, rel=0
  , amp*=envelope{d:50 + ((this.ohdecay*1000+250)*this.o) + (this.chdecay*200*(1-this.o)), units:'ms', shape:'exp'}
  , rate=1


