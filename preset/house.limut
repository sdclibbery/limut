include 'preset/909.limut'

preset tb303 wave
	, wave='saw', oct=2+this.u-this.d, choke=this.player
	, dur=1/4 // 1/16th notes
  , envelope='organ', sus={this.dur-(1-this.a)*5/48, level:1} // Gate
  , amp*=envelope{d:1000}*3/4 // VCA envelope: slow exponential decay
  // 303 Controls
  , cutoff=1/2
  , resonance=1/2
  , envmod=1/2
	, decay=1/2
  , accent=1/2
  // Derived params
  , amp*=3/4 + this.a*this.accent/3
  , meg=envelope{d:340+(this.decay^2)*(1-this.a)*2000} // 303 Main Envelope Generator
  , smoothedmeg=smooth{this.meg*this.a*128,att:2^-6,dec:2^-16+(1-this.a)*100}*this.resonance + this.meg*(1-this.resonance) // Smoothed meg for the accent sweep, based on resonance
  , accentedmeg=this.smoothedmeg*this.a*this.accent + this.meg*(1-this.a*this.accent) // Accent sweep uses smoothed meg
  , appliedmeg=(this.envmod+1)*this.accentedmeg + 1-this.envmod // Apply envmod
  , lpf={
    80 + (160 + 1700*this.cutoff^3) * this.appliedmeg,
    q:this.resonance*25,
    poles:4
  }
  , glide={(this.g+this.s)*88, units:'ms'}

preset m1organ multiwave
	, dur=1/4 // 16th notes for syncopation
  , envelope='full', att=0, dec=1/8, sus={level:7/8}, rel=1/8, amp*=3
  , cutoff=1/2
  , wave1={'sine',detune:-24}
  , wave2={'square',detune:7,amp:1/4}
  , wave3={'noise',detune:-3*12,amp:envelope{d:10}/3}
  , lpf={500*(1/2+this.cutoff)*(1+envelope{d:500}),poles:4,q:10}

preset didgeridoo square 0, dur=1, envelope='pad', amp=[0.3:0.5]n3@f^2 + [0:0.3]l1@f, oct=2, clip, phaser=1/4, ring=1/4, glide=this.dur/2
