include 'lib/nodes.limut'

// This library includes more complex effect units

set flanger = {control:[0,1]l8@f,feedback:0,lo:0.0005,hi:0.007} ->
  feedback>0
  ?? {delay{0.01,max:0.01},delay{0.01+lo+control^2*(hi-lo),feedback:gain{feedback*0.8},max:0.1}}/2
  ?: {idnode,delay{lo+control^2*(hi-lo),max:0.1}}/2

set phaser = {control:[0,1]l4@f,lo:300,hi:2600,stages:4,q:1/2} -> {
  idnode,
  series{
   	apf{freq:lo+control^2*(hi-lo),q:q},
    stages
  }
}/2

set grain = {ratio, length:0.03, phase:0} ->
delay{ lfo{ // Moving delay to change the pitch
  wave: 'sawtooth',
	freq: 1/length,
  lo: ratio>=1 ?? length*(ratio-1)*1.2 ?: 0,
  hi: ratio>=1 ?? 0 ?: length*(1-ratio)*1.2,
  phase: phase
}, max:0.1 }
>> gain{ // cosine envelope to silence the distortion when the delay has to reset
	osc{'saw',freq:1/length,phase:phase }
	>>shaper{{x}->cos{pi*x*0.6},oversample:'none'}
}

set shifter = {ratio, length:0.03} ->{ // Multiple overlapping pitch shifting grains
	grain{ratio,length:length,phase:0/2},
  grain{ratio,length:length,phase:1/2}
}*0.825

set ultracomb = {f:smooth{[]r1@f,att:1},p:smooth{[]r2/3@f,att:1},s:smooth{[]r1/2@f,att:1},stages:6} -> series{{
  idnode,
  delay{0.0002 + f^2*0.007,max:0.1} - delay{0.004 + f^2*0.01,max:0.1}, // Flanger part
  series{apf{300 + p^2*2000},6}, // Phaser part
  shifter{0.95 + s*0.1} // Shifter part
}/3.3,stages}

set reverb = {length:1b,curve:3} -> convolver{
  env: {
    l:{x,c:curve} -> (1-x)^c,
    r:{x,c:curve} -> (1-x)^c
  },
  length:length
}

set echo = {time:1/8b,feedback:0.7,max} -> {idnode,delay{time,feedback:feedback,max:max}}

