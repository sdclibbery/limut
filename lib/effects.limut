include 'lib/nodes.limut'

// This library includes more complex effect units

set flanger = {control:[0,1]l8@f,feedback:0,lo:0.0002,hi:0.007} ->
  feedback>0
  ?? {delay{0.01},delay{0.01+lo+control*hi,feedback:gain{feedback*0.8}}}/2
  ?: {idnode,delay{lo+control*hi}}/2

set grain = {ratio, length:0.03, phase:0} ->
delay{ lfo{ // Moving delay to change the pitch
  wave: 'sawtooth',
	freq: 1/length,
  lo: ratio>=1 ?? length*(ratio-1)*1.2 ?: 0,
  hi: ratio>=1 ?? 0 ?: length*(1-ratio)*1.2,
  phase: phase
} }
>> gain{ // cosine envelope to silence the distortion when the delay has to reset
	osc{'saw',freq:1/length,phase:phase }
	>>shaper{{x}->cos{pi*x*0.6},oversample:'none'}
}

set shifter = {ratio, length:0.03} ->{ // Multiple overlapping pitch shifting grains
	grain{ratio,length:length,phase:0/2},
  grain{ratio,length:length,phase:1/2}
}*0.825

set reverb = {length:1b,curve:3} -> convolver{
  env: {
    l:{x,c:curve} -> (1-x)^c,
    r:{x,c:curve} -> (1-x)^c
  },
  length:length
}
